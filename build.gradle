import org.jetbrains.kotlin.gradle.tasks.KotlinCompile

plugins {
	id 'net.fabricmc.fabric-loom' version "${loom_version}"
	id 'maven-publish'
	id "org.jetbrains.kotlin.jvm" version "2.3.10"
}

version = project.mod_version
group = project.maven_group

base {
	archivesName = project.archives_base_name
}

repositories {
	// Add repositories to retrieve artifacts from in here.
	// You should only use this when depending on other mods because
	// Loom adds the essential maven repositories to download Minecraft and libraries from automatically.
	// See https://docs.gradle.org/current/userguide/declaring_repositories.html
	// for more information about repositories.
}

loom {
	splitEnvironmentSourceSets()

	mods {
		"kts4mc" {
			sourceSet sourceSets.main
			sourceSet sourceSets.client
		}
	}

	accessWidenerPath = file("src/main/resources/katton.accesswidener")

}

dependencies {
	include(implementation('org.jetbrains.kotlin:kotlin-script-runtime'))
	include(implementation('org.jetbrains.kotlin:kotlin-reflect'))
	include(implementation('org.jetbrains.kotlin:kotlin-scripting-common'))
	include(implementation('org.jetbrains.kotlin:kotlin-scripting-jvm'))
	include(implementation('org.jetbrains.kotlin:kotlin-scripting-jvm-host'))
	include(implementation('org.jetbrains.kotlin:kotlin-scripting-compiler-embeddable'))
	include(implementation('org.jetbrains.kotlin:kotlin-scripting-compiler-impl-embeddable'))
	include(implementation('org.jetbrains.kotlin:kotlin-compiler-embeddable'))
	include(implementation("org.jetbrains.kotlinx:kotlinx-coroutines-core-jvm:1.8.0"))
	include(implementation("org.jetbrains.kotlinx:kotlinx-coroutines-jdk8:1.8.0"))

	// To change the versions see the gradle.properties file
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	implementation "net.fabricmc:fabric-loader:${project.loader_version}"

	// Fabric API. This is technically optional, but you probably want it anyway.
	implementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_api_version}"
}

processResources {
	inputs.property "version", project.version

	filesMatching("fabric.mod.json") {
		expand "version": inputs.properties.version
	}
}

tasks.withType(JavaCompile).configureEach {
	it.options.release = 25
}

tasks.withType(KotlinCompile).all {
	kotlinOptions {
		jvmTarget = 25
	}
}

java {
	// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
	// if it is present.
	// If you remove this line, sources will not be generated.
	withSourcesJar()

	sourceCompatibility = JavaVersion.VERSION_25
	targetCompatibility = JavaVersion.VERSION_25
}

jar {
	inputs.property "archivesName", project.base.archivesName

	from("LICENSE") {
		rename { "${it}_${inputs.properties.archivesName}"}
	}
}

// configure the maven publication
publishing {
	publications {
		create("mavenJava", MavenPublication) {
			artifactId = project.archives_base_name
			from components.java
		}
	}

	// See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
	repositories {
		// Add repositories to publish to here.
		// Notice: This block does NOT have the same function as the block in the top level.
		// The repositories here will be used for publishing your artifact, not for
		// retrieving dependencies.
	}
}

def datapacksDir = layout.projectDirectory.dir('Example/datapacks')
def targetDatapacksDir = layout.projectDirectory.dir('run/saves/test/datapacks')

// 任务：将 Example/datapacks 复制并合并到 run/saves/test/datapacks
// Example/datapacks 优先级更高，会覆盖目标目录中的同名文件
tasks.register('copyDatapacks', Copy) {
    group = 'Katton'
    description = 'Copy and merge datapacks from Example/datapacks to run/saves/新的世界/datapacks'

    from datapacksDir
    into targetDatapacksDir

    // 如果文件冲突，使用 Example/datapacks 的文件（覆盖目标文件）
    duplicatesStrategy = DuplicatesStrategy.INCLUDE
    // 显示复制的文件
    eachFile { details ->
        logger.lifecycle("Copying datapack: ${details.sourcePath}")
    }
}

// 任务：清理并强制重新复制所有 datapacks
tasks.register('cleanDatapacks') {
    group = 'Katton'
    description = 'Clean target datapacks directory'

    doFirst {
        // 删除目标目录
        def targetDir = targetDatapacksDir.asFile
        if (targetDir.exists()) {
            logger.lifecycle("Cleaning target directory: ${targetDir.absolutePath}")
            delete targetDir
        }
    }
}
